{
  parserClass="org.elixir_lang.parser.ElixirParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Elixir"
  psiImplClassSuffix="Impl"
  psiPackage="org.elixir_lang.psi"
  psiImplPackage="org.elixir_lang.psi.impl"

  elementTypeHolderClass="org.elixir_lang.psi.ElixirTypes"
  elementTypeClass="org.elixir_lang.psi.ElixirElementType"
  tokenTypeClass="org.elixir_lang.psi.ElixirTokenType"

  tokens = [
    COMMENT = "regexp:#[^\r\n]*(\n|\r|\r\n)?"
  ]
}

elixirFile ::= expressionList

// In alphabetical order

charList ::= SINGLE_QUOTE
             interpolatedCharListBody
             SINGLE_QUOTE
charListHeredoc ::= TRIPLE_SINGLE_QUOTE EOL
                    interpolatedCharListBody
                    TRIPLE_SINGLE_QUOTE
private interpolatedCharListBody ::= (interpolation | CHAR_LIST_FRAGMENT | VALID_ESCAPE_SEQUENCE)*
private interpolatedSigilBody ::= (interpolation | SIGIL_FRAGMENT | VALID_ESCAPE_SEQUENCE)*
private interpolatedStringBody ::=  (interpolation | STRING_FRAGMENT | VALID_ESCAPE_SEQUENCE)*
private interpolatedSigil ::= TILDE SIGIL_INTERPOLATING_NAME TRIPLE_DOUBLE_QUOTES EOL
                              interpolatedSigilBody
                              TRIPLE_DOUBLE_QUOTES
private literalSigil ::= TILDE SIGIL_LITERAL_NAME TRIPLE_DOUBLE_QUOTES EOL
                         literalSigilBody
                         TRIPLE_DOUBLE_QUOTES
private literalSigilBody ::= SIGIL_FRAGMENT*
stringHeredoc ::= TRIPLE_DOUBLE_QUOTES EOL
                  interpolatedStringBody
                  TRIPLE_DOUBLE_QUOTES
private expression ::= (NUMBER | charList | charListHeredoc | sigil | string | stringHeredoc)
private expressionList ::= expression (EOL expression)* EOL?
interpolation ::= INTERPOLATION_START expressionList? INTERPOLATION_END
sigil ::= interpolatedSigil | literalSigil
string ::= DOUBLE_QUOTES
           interpolatedStringBody
           DOUBLE_QUOTES