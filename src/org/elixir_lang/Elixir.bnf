{
  parserClass="org.elixir_lang.parser.ElixirParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Elixir"
  psiImplClassSuffix="Impl"
  psiPackage="org.elixir_lang.psi"
  psiImplPackage="org.elixir_lang.psi.impl"

  elementTypeHolderClass="org.elixir_lang.psi.ElixirTypes"
  elementTypeClass="org.elixir_lang.psi.ElixirElementType"
  tokenTypeClass="org.elixir_lang.psi.ElixirTokenType"

  tokens = [
    COMMENT = "regexp:#[^\r\n]*(\n|\r|\r\n)?"
  ]
}

elixirFile ::= expressionList

// In alphabetical order

charList ::= SINGLE_QUOTE
             interpolatedCharListBody
             SINGLE_QUOTE
charListHeredoc ::= TRIPLE_SINGLE_QUOTE EOL
                    interpolatedCharListBody
                    TRIPLE_SINGLE_QUOTE
private interpolatedCharListBody ::= (interpolation | CHAR_LIST_FRAGMENT | VALID_ESCAPE_SEQUENCE)*
interpolatedString ::= DOUBLE_QUOTES
                       interpolatedStringBody
                       DOUBLE_QUOTES
private interpolatedStringBody ::=  (interpolation | INTERPOLATED_STRING_FRAGMENT | VALID_ESCAPE_SEQUENCE)*
interpolatedHeredoc ::= TRIPLE_DOUBLE_QUOTES EOL
                        interpolatedStringBody
                        TRIPLE_DOUBLE_QUOTES
private expression ::= (NUMBER | charList | charListHeredoc | interpolatedString | interpolatedHeredoc)
private expressionList ::= expression (EOL expression)* EOL?
interpolation ::= INTERPOLATION_START expressionList? INTERPOLATION_END
